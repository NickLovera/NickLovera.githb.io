<!DOCTYPE html>
<html>
    <pre><code>
#Author: Nicholas Lovera
#Purose: Homework_3 Assembly Language
#Description: The user is trying to guess a series of 3 numbers
#a strike means the user guessed a number correctly and a ball means
#the user guessed a number incorrectly though they do not know which ones

.data
userinput:  .space 12
userinput1: .space 3
type_3: 	.asciiz "\nType 3 Numbers: (Each followed by an enter)\n"
strikes: 	.asciiz " strike(s) and "
win:		.asciiz "Strike Out!\n"
balls: 		.asciiz " ball(s)\n"
tries:		.asciiz "Tries: "
menu_1:		.asciiz "\n\t    M E N U\n"
menu_2:		.asciiz "\tPlay New Game (p)\n"
menu_3:		.asciiz "\tQuit (q)\n"
menu_4:		.asciiz "\n\tChoose? "
numbers:	.asciiz "Which number would you like to guess?\n(1)\n(2)\n(3)"
goodbye:	.asciiz "Thanks for playing Good-Bye"
number_1:	.word 4, 8, 6
number_2:	.word 7, 1, 3		#Number user is trying to guess on 2nd play and after
number_3:	.word 9, 0, 5
main_array: .word 4, 8, 6		#Number user is trying to guess on 1st play

.text
.globl main


#Main menu loop
main: 
	beq $0, $a0, exit  			#Menu loop
	
	la $a0, menu_1		 	
	li $v0, 4			 	
	syscall						#Print menu_1
	
	la $a0, menu_2		 	
	li $v0, 4			 	
	syscall						#Print menu_2
	
	la $a0, menu_3		 	
	li $v0, 4			 	
	syscall						#Print menu_3
	
	la $a0, menu_4		 	
	li $v0, 4			 	
	syscall						#Print menu_4
	
	li $v0, 8		   	 	
	la $a0, userinput1
	li $a1, 3
	syscall						#Get user input
	
	
	lb $t4, 0($a0)		 		#Load users input byte to $t4
	beq $t4, 'p', new_guess	 	#Go to begin if users input is p
	
	move $a0, $0		 		#if q or any other input
	j main						#	set clear condition to break loop
	
#Set registers and get ready to take in users input
new_guess:
	
	jal set_variables			#Set registers to be used
	
	la $a0, type_3	 			#Load type_3 address
	li $v0,4					#Print type_3
	syscall
	
	j user_input				#Jump to get users input

#Setting registers for each guess
set_variables:
	li $s2, 0					#Strikes counter
	li $s3, 0					#balls counter
	li $s4, 0 					#for_inner counter	(i=0)
	li $t4, 0					#for_outter counter (i=0)
	
	jr $ra
	
#Take in the user input and store in userinput
user_input:	
	li $v0, 5
	syscall					
	sw $v0, userinput 			#Storing 1st digit
	li $v0, 5
	syscall					
	sw $v0, userinput + 4		#Storing 2nd digit
	li $v0, 5
	syscall						
	sw $v0, userinput + 8		#Storing 3rd digit
	
	j input_done

#User is done get ready for comparison
input_done:
	bne $s5, 1, first
	beq $s5, 1, second
	#la $t1, main_array			#Loading address of main_array
	#addi $t7, $t7, 1			#Increment tries counter 
	#j for_outter				#Jump to start comparison loop

#Setting number to be guesssed(main_array)
first:
	la $t1, main_array			#Loading address of main_array
	addi $t7, $t7, 1			#Increment tries counter 
	j for_outter				#Jump to start comparison loop

#Setting number to be guessed (number_2)
second:
	la $t1, number_2			#Loading address of number_2
	addi $t7, $t7, 1			#Increment tries counter 
	j for_outter				#Jump to start comparison loop

#Beginning of outter for loop
for_outter:
	beq $t4, 3, done_outter		#Branch if reached the end of the array
	
	la $t2, userinput			#Loading address of array_2
	lw $t5, 0($t1)				#Loading element from main_array
	j for_inner					#Jump to start inner for loop
	
#Beginning of inner for loop
for_inner:
	beq $s2, 3, done_inner		#Branch if reached the end of the array
	
	lw $t6, 0($t2)				#Loading elemment from array_user
	bne $t5, $t6, next_inner	#Branch if elements are not equal
	
	j strike_ball				#They are equal determin if strike/ball

#Incrementing array_user and inner loop counter
next_inner:
	
	addi $t2, $t2, 4			#Incrementing array_user (B[i+1])
	addi $s2, $s2, 1			#Incrementing for_inner (i+1)
	
	j for_inner					#Go to beggining of inner loop
	
#Incrementing main_array and outter loop counter	
next_outter:
	addi $t1, $t1, 4			#Incrementing main_array (A[i+1])
	addi $t4, $t4, 1			#Incrementing for_outter counter (i+1)
	
	j for_outter				#Go to beggining of outter loop

#Determine if strike or ball
strike_ball:
	bne $s2,$t4,ball			#They are are in different spots (Ball)
	
	beq $s2,$t4,strike			#They are in the same spot (Strike)
	
#Increment and go back to inner loop
ball:
	addi $s3,$s3,1				#incrementing Ball counter
	j next_inner				#Go to next_outter
	
#Increment and go back to inner loop
strike:
	addi $s4, $s4, 1			#Incrrementing Strike counter
	j next_inner				#Go to next_inner

#Inner loop is done reset counter and go to outter loop	
done_inner: 
	li $s2, 0			 		#Reset counter (i)
	j next_outter				#Go to next_outter
	
#The two numbers have been compared 
#Strikes and balls have been determined
done_outter:
	li $t4, 0					#Reset outter loop counter
	
	beq $s4, 3, winner			#Branch if strikes == 3 (Win)
	
	li $v0, 1					
	move $a0, $s4				
	syscall						#Print (#) of strikes
	
	la $a0, strikes			
	li $v0, 4					
	syscall						#Print strikes string
	
	move $a0, $s3				
	li $v0, 1					
	syscall						#Print (#) of balls
	
	la $a0, balls
	li $v0, 4
	syscall 					#Print balls string
	
	j new_guess					#Jump to let the user guess again

#Print win string, # of tries, and return to main menu	
winner:
	la $a0, win
	li $v0,4
	syscall						#Print win string
	
	la $a0, tries			
	li $v0, 4					
	syscall						#Print tries string
	
	move $a0, $t7				
	li $v0, 1					
	syscall						#Print # of tries
	
	li $s5, 1					#Set condition for second
	li $t7, 0					#Resetting tries counter
	j main						#User won return to main menu
	

#Say goodbye and exit the program
exit: 
	la $a0, goodbye 		
	li $v0, 4
	syscall						#Print goodbye string

	li $v0, 10				
	syscall						#Exit program
    </code></pre>
</html>